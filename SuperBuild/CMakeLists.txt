cmake_minimum_required(VERSION 2.8)
project(BRAINSImageEvalSuperBuild)

include(ExternalProject)

set(COMMON_CMAKE_ARGS
  -DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}
  -DCMAKE_C_COMPILER:STRING=${CMAKE_C_COMPILER}
  -DCMAKE_CXX_COMPILER:STRING=${CMAKE_CXX_COMPILER}
  -DBUILD_SHARED_LIBS:BOOL=OFF
  )
message("COMMON_CMAKE_ARGS=${COMMON_CMAKE_ARGS}")
set(FASTBUILD_CMAKE_ARGS -DBUILD_EXAMPLES:BOOL=OFF
  -DBUILD_TESTING:BOOL=OFF
  )

find_package(Qt4 COMPONENTS QtCore QtGui QtNetwork REQUIRED)
message("QT_USE_FILE=${QT_USE_FILE}")
include(${QT_USE_FILE})

set(ITK_GIT_TAG a651d3a6d3d15546ae437a3e71edf35e3e92b4c9)

ExternalProject_Add(ITK
  GIT_REPOSITORY git://itk.org/ITK.git
#  GIT_TAG 9d020d5682cb66dce7b3245784b5bc21055b2b01
  GIT_TAG ${ITK_GIT_TAG}
  UPDATE_COMMAND ""
  INSTALL_COMMAND ""
  CMAKE_ARGS
  ${COMMON_CMAKE_ARGS}
  ${FASTBUILD_CMAKE_ARGS}
  -DITK_BUILD_ALL_MODULES:BOOL=ON
  )
set(ITK_DIR ${CMAKE_CURRENT_BINARY_DIR}/ITK-prefix/src/ITK-build)
set(VTK_QT_ARGS)
if(NOT APPLE)
  set(VTK_QT_ARGS
    -DVTK_USE_QT:BOOL=ON
    -DDESIRED_QT_VERSION:STRING=4
    -DVTK_USE_GUISUPPORT:BOOL=ON
    -DVTK_USE_QVTK_QTOPENGL:BOOL=ON
    -DVTK_USE_RENDERING:BOOL=ON
    -DQT_QMAKE_EXECUTABLE:FILEPATH=${QT_QMAKE_EXECUTABLE}
    -DVTK_USE_PARALLEL:BOOL=ON
    )
else()
  set(VTK_QT_ARGS
    -DVTK_REQUIRED_OBJCXX_FLAGS:STRING=""
    -DVTK_USE_QT:BOOL=ON
    -DVTK_USE_CARBON:BOOL=OFF
    -DVTK_USE_COCOA:BOOL=ON # Default to Cocoa, VTK/CMakeLists.txt will enable Carbon and disable cocoa if needed
    -DVTK_USE_X:BOOL=OFF
    -DVTK_USE_RPATH:BOOL=ON
    -DDESIRED_QT_VERSION:STRING=4
    -DVTK_USE_GUISUPPORT:BOOL=ON
    -DVTK_USE_QVTK_QTOPENGL:BOOL=ON
    -DVTK_USE_RENDERING:BOOL=ON
    -DQT_QMAKE_EXECUTABLE:FILEPATH=${QT_QMAKE_EXECUTABLE}
    -DVTK_USE_PARALLEL:BOOL=ON
    )
endif()

ExternalProject_Add(VTK
  GIT_REPOSITORY "git://vtk.org/VTK.git"
  GIT_TAG "v5.8.0"
  UPDATE_COMMAND ""
  INSTALL_COMMAND ""
  CMAKE_ARGS
  ${COMMON_CMAKE_ARGS}
  ${FASTBUILD_CMAKE_ARGS}
  ${VTK_QT_ARGS}
  DEPENDS
  )
set(VTK_DIR ${CMAKE_CURRENT_BINARY_DIR}/VTK-prefix/src/VTK-build)

ExternalProject_Add(BRAINSImageEval
  DOWNLOAD_COMMAND ""
  SOURCE_DIR ${CMAKE_CURRENT_LIST_DIR}/..
  BINARY_DIR ${CMAKE_CURRENT_BINARY_DIR}/BRAINSImageEval-build
  CMAKE_ARGS
  ${COMMON_CMAKE_ARGS}
  -DQT_QMAKE_EXECUTABLE:FILEPATH=${QT_QMAKE_EXECUTABLE}
  -DITK_DIR:PATH=${ITK_DIR}
  -DVTK_DIR:PATH=${VTK_DIR}
  INSTALL_COMMAND ""
  DEPENDS ITK VTK
  )
